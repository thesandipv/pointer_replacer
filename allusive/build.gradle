/*
 * Copyright (C) 2016-2020 Sandip Vaghela
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *         http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'kotlin-kapt'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
//    id 'com.google.android.gms.oss-licenses-plugin'
}

apply from: "$rootDir/gradle/common-config.gradle"

ext {
    major = 1
    minor = 9
    patch = 3
    minimumSdkVersion = 16
    ci = System.getenv("CI") == "true"
}
android {
    buildFeatures {
        dataBinding true
    }

    defaultConfig {
        applicationId "com.afterroot.allusive2"
        targetSdkVersion SDK.TARGET
        versionCode VERSION_CODE
        versionName VERSION_NAME
        vectorDrawables {
            useSupportLibrary = true
        }
        multiDexEnabled true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += [
                        "room.schemaLocation": "$projectDir/schemas".toString(),
                        "room.incremental"   : "true"
                ]
            }
        }

        def commit = "git rev-list --max-count=1 HEAD".execute([], project.rootDir).text.substring(0, 7)
        buildConfigField "String", "COMMIT_ID", "\"$commit\""
    }

    def keystorePropertiesFile = rootProject.file("keystore.properties")
    def keystoreProperties = new Properties()
    if (keystorePropertiesFile.exists()) {
        keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
    }

    signingConfigs {
        allusive {
            keyAlias "allusive"
            keyPassword keystoreProperties['keyPassword'] ?: System.getenv("SIGN_KEY_PW")
            storeFile rootProject.file("release/keystore.jks")
            storePassword keystoreProperties['storePassword'] ?: System.getenv("SIGN_STORE_PW")
        }
    }

    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            multiDexKeepProguard file('proguard-rules.pro')
            multiDexKeepFile file('main_dex.txt')
            signingConfig signingConfigs.allusive
        }
        debug {
            ext.alwaysUpdateBuildId = false
            splits.abi.enable = false
            splits.density.enable = false
            crunchPngs false

            signingConfig signingConfigs.allusive

            versionNameSuffix = '-beta'
        }
    }

    packagingOptions {
        // Exclude AndroidX version files
        exclude 'META-INF/*.version'
        // Exclude consumer proguard files
        exclude 'META-INF/proguard/*'
        // Exclude the Firebase/Fabric/other random properties files
        exclude '/*.properties'
        exclude 'fabric/*.properties'
        exclude 'META-INF/*.properties'
    }

    dexOptions {
        javaMaxHeapSize "4g"
        preDexLibraries = false
    }

    lintOptions {
        abortOnError false
    }

    packagingOptions {
        // Exclude AndroidX version files
        exclude 'META-INF/*.version'
        // Exclude consumer proguard files
        exclude 'META-INF/proguard/*'
        // Exclude the Firebase/Fabric/other random properties files
        exclude '/*.properties'
        exclude 'fabric/*.properties'
        exclude 'META-INF/*.properties'
    }

    sourceSets {
        // Adds exported schema location as test app assets.
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }
}


dependencies {
    implementation project(":data")
    implementation project(":ui:magisk")
    implementation project(":ui:resources")

    implementation Libs.AfterROOT.Core.core

    implementation Libs.Kotlin.anko

    implementation Libs.AndroidX.appCompat
    implementation Libs.AndroidX.billing
    implementation Libs.AndroidX.cardView
    implementation Libs.AndroidX.constraintLayout
    implementation Libs.AndroidX.fragment
    implementation Libs.AndroidX.Lifecycle.common
    implementation Libs.AndroidX.Lifecycle.extensions
    implementation Libs.AndroidX.Lifecycle.livedata
    implementation Libs.AndroidX.Lifecycle.viewmodel
    implementation Libs.AndroidX.multiDex
    implementation Libs.AndroidX.Navigation.fragment
    implementation Libs.AndroidX.Navigation.ui
    implementation Libs.AndroidX.paging
    implementation Libs.AndroidX.palette
    implementation Libs.AndroidX.preference
    implementation Libs.AndroidX.recyclerView
    implementation Libs.AndroidX.supportV13
    implementation Libs.AndroidX.supportV4
    implementation Libs.AndroidX.vectorDrawable

    implementation Libs.AndroidX.Room.room
    implementation Libs.AndroidX.Room.runtime
    kapt Libs.AndroidX.Room.compiler
    androidTestImplementation Libs.AndroidX.Room.testing

    implementation Libs.MaterialDialogs.input
    implementation Libs.MaterialDialogs.core
    implementation Libs.MaterialDialogs.bottomSheets
    implementation Libs.MaterialDialogs.color

    implementation Libs.FirebaseUI.auth
    implementation Libs.FirebaseUI.firestore
    implementation Libs.FirebaseUI.storage

    implementation Libs.Glide.glide
    kapt Libs.Glide.compiler

    implementation platform(Libs.Firebase.bom)

    implementation Libs.Google.ossLic
    implementation Libs.Google.material
    implementation Libs.Firebase.ads
    implementation Libs.Firebase.analytics
    implementation Libs.Firebase.auth
    implementation Libs.Firebase.config
    implementation Libs.Firebase.core
    implementation Libs.Firebase.crashlytics
    implementation Libs.Firebase.database
    implementation Libs.Firebase.firestore
    implementation Libs.Firebase.messaging
    implementation Libs.Firebase.storage

    implementation 'com.google.android.play:core:1.10.0'
    implementation Libs.Google.gson

    implementation Libs.commonsIo
    compileOnly "de.robv.android.xposed:api:82"

    implementation Libs.Koin3.android
    implementation Libs.Koin3.ext
    testImplementation Libs.Koin3.test

    implementation 'com.airbnb.android:epoxy:4.6.4'
    kapt 'com.airbnb.android:epoxy-processor:4.6.1'

    implementation Libs.Kotlin.Coroutines.core
    implementation Libs.Kotlin.Coroutines.android
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.0'

    testImplementation Libs.Test.junit
    androidTestImplementation Libs.Test.junitExt
    androidTestImplementation Libs.Test.espresso

    testImplementation 'androidx.test:core:1.3.0'

    implementation 'com.squareup.okhttp3:okhttp:5.0.0-alpha.2'
    implementation 'me.zhanghai.android.fastscroll:library:1.1.5'
}