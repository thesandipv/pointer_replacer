/*
 * Copyright (C) 2016-2020 Sandip Vaghela
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *         http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'kotlin-kapt'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
    id 'com.google.android.gms.oss-licenses-plugin'
}

kapt {
    correctErrorTypes = true
    useBuildCache = true
}

apply from: "$rootDir/gradle/oss-licence.gradle"
apply from: "$rootDir/gradle/apply-core.gradle"

ext {
//    major = 1
//    minor = 9
//    patch = 3
//    minimumSdkVersion = 16
    ci = System.getenv("CI") == "true"
}
android {
    compileSdkVersion SDK.COMPILE

    buildFeatures {
        viewBinding true
        dataBinding true
    }

    defaultConfig {
        applicationId "com.afterroot.allusive2"
        minSdkVersion SDK.MIN
        targetSdkVersion SDK.TARGET
        versionCode VERSION_CODE
        versionName VERSION_NAME
        vectorDrawables {
            useSupportLibrary = true
        }
        multiDexEnabled true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += [
                        "room.schemaLocation": "$projectDir/schemas".toString(),
                        "room.incremental"   : "true"
                ]
            }
        }

        manifestPlaceholders = [
                hostName  : "afterroot.web.app",
                pathPrefix: "/apps/pointer-replacer/launch"
        ]

        resConfigs "en"

        def commit = System.getenv("COMMIT_ID") ?: "git rev-list --max-count=1 HEAD".execute([], project.rootDir).text.substring(0, 7)
        buildConfigField "String", "COMMIT_ID", "\"$commit\""
    }

    def keystorePropertiesFile = rootProject.file("keystore.properties")
    def keystoreProperties = new Properties()
    if (keystorePropertiesFile.exists()) {
        keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
    }

    signingConfigs {
        allusive {
            keyAlias "allusive"
            keyPassword keystoreProperties['keyPassword'] ?: System.getenv("SIGN_KEY_PW")
            storeFile rootProject.file("release/keystore.jks")
            storePassword keystoreProperties['storePassword'] ?: System.getenv("SIGN_STORE_PW")
        }
    }

    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            multiDexKeepProguard file('proguard-rules.pro')
            multiDexKeepFile file('main_dex.txt')
            signingConfig signingConfigs.allusive
        }
        debug {
            ext.alwaysUpdateBuildId = false
            splits.abi.enable = false
            splits.density.enable = false
            crunchPngs false

            signingConfig signingConfigs.allusive

            versionNameSuffix = '-beta'
        }
    }

    lintOptions {
        abortOnError false
    }

    packagingOptions {
        // Exclude AndroidX version files
        exclude 'META-INF/*.version'
        // Exclude consumer proguard files
        exclude 'META-INF/proguard/*'
        // Exclude the Firebase/Fabric/other random properties files
        exclude '/*.properties'
        exclude 'fabric/*.properties'
        exclude 'META-INF/*.properties'
        exclude "DebugProbesKt.bin" //Exclude coroutines debug file
    }

    sourceSets {
        // Adds exported schema location as test app assets.
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }

    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    lintOptions {
        abortOnError false
    }
}


dependencies {
    implementation project(":base")
    implementation project(":data")
    implementation project(":ui:magisk")
    implementation project(":ui:repo")
    implementation project(":ui:resources")

    implementation libs.kotlin.stdLib

    implementation libs.androidx.appCompat
    implementation libs.androidx.billing
    implementation libs.androidx.cardView
    implementation libs.androidx.constraintLayout
    implementation libs.androidx.fragment
    implementation libs.bundles.lifecycle
    implementation libs.androidx.multiDex
    implementation libs.androidx.navigation.fragment
    implementation libs.androidx.navigation.ui
    implementation libs.androidx.paging
    implementation libs.androidx.palette
    implementation libs.androidx.preference
    implementation libs.androidx.recyclerView
    implementation libs.androidx.supportV13
    implementation libs.androidx.supportV4
    implementation libs.androidx.vectorDrawable

    implementation libs.androidx.room.room
    implementation libs.androidx.room.runtime
    kapt libs.androidx.room.compiler
    androidTestImplementation libs.androidx.room.test

    implementation libs.materialdialogs.input
    implementation libs.materialdialogs.core
    implementation libs.materialdialogs.bottomSheets
    implementation libs.materialdialogs.color

    implementation libs.firebase.ui.auth
    implementation libs.firebase.ui.firestore
    implementation libs.firebase.ui.storage

    kapt libs.glide.compiler

    implementation libs.google.ossLic
    implementation libs.google.material
    implementation libs.google.ads
    implementation libs.bundles.firebase

    implementation 'com.google.android.play:core:1.10.3'
    implementation libs.google.gson

    implementation libs.commonsIo
    compileOnly "de.robv.android.xposed:api:82"

    implementation libs.koin.android
    implementation libs.koin.ext
    testImplementation libs.koin.test

    implementation 'com.airbnb.android:epoxy:4.6.4'
    kapt 'com.airbnb.android:epoxy-processor:4.6.1'

    implementation libs.kotlin.coroutines.core
    implementation libs.kotlin.coroutines.android
    testImplementation libs.kotlin.coroutines.test

    testImplementation libs.test.junit
    androidTestImplementation libs.androidx.test.junitExt
    androidTestImplementation libs.androidx.test.espresso

    testImplementation libs.androidx.test.core

    implementation 'com.squareup.okhttp3:okhttp:5.0.0-alpha.3'
    implementation 'me.zhanghai.android.fastscroll:library:1.1.5'
}
